_SET_BPM 130;
_DECLARE_CHANNEL 1;
_DECLARE_CHANNEL 2;
_DECLARE_CHANNEL 3;
_DECLARE_CHANNEL 4;
_SET_INSTRUMENT 1 62;
_SET_INSTRUMENT 2 81;
_SET_INSTRUMENT 3 128;
_SET_INSTRUMENT 4 117;

oneBarRest = "[.:4]" ** 4;
fourBarRest = oneBarRest ** 4;
oneBarDrumLoop = "[C4:8, C4:8, {C4/D4/E4/F4}:8, C4:8]" ** 2;
twoBarDrumLoopDotted = "[{C4/D4/E4/F4}:8, C4:8, C4:8]" ** 4 ++ "[{C4/D4/E4/F4}:8, C4:8]" ** 2;
fourBarDrumLoop =  oneBarDrumLoop ** 4;

bassIntro = "[C2:1]" ** 2 ++ "[C2:2, C2:4, .:8, F2:2]" ++ "[D3:4dot, .:4]";
bass8ths = ("[C2:8]" ** 8) ** 2 ++ "[C2:8]" ** 7 ++ "[F2:8]" ** 4 ++ "[G2:8]" ** 5;

synthChords1 = "[.:4, {G4/B4/D5}:8, .:4, {G4/C5/E5}:8, .:4]"
  ++ "[{F3/A4/C5}:8, .:8, .:8, {F3/A4/C5}:8, .:8, {G4/B4/D5}:8, .:8, {G4/B4/D5}:4dot, {G4/C5/E5}:8, .:4, {F3/A4/C5}:8, .:8, {C3/F3/A4}:4, {C3/E3/G4}:4, {C3/D3/A4}:2, .:8]";
synthChords2 = "[.:4, {F5/A5/C6}:8, .:4, {F5/A5/C6}:8, .:4, {F5/A5/C6}:8, .:4, {F5/A5/C6}:8, .:4, {C4/E5/G5}:4]"
  ++ "[.:4, {F3/A4/C4}:8, .:4, {C3/F3/A4}:8, .:8, {C3/F3/A4}:4, {E3/G3}:8, .:8, {C3/D3/G4}:2, .:8]";

songIntroBass = bassIntro ** 2 ++ bass8ths ** 2;
songIntroSynth = synthChords1 ** 3 ++ synthChords2;
songIntroToms = ("[.:4]" ** 14 ++ "[.:8, G5:8, C5:8, C4:8]") 
  ++ ("[C3:4]" ++ "[.:4]" ** 13 ++ "[.:8, G5:8, C5:8, C4:8]") ** 2 
  ++ "[.:4]" ** 14 ++ "[.:8, G5:8, C5:8, C4:8]"; 

songMiddleSynth = synthChords1 ++ ("[{A4/C5}:4dot, {G4/B4}:4dot]" ** 2 ++ "[{A4/C5}:4, {G4/B4}:4]");
for (i = 0; i < 3; i = i + 1) do
  lift = "[E5:4, D5:2dot, .:8]";
  if (i == 2) do
    songMiddleSynth = songMiddleSynth ++ "[F4:8, A4:8, C5:8, A4:8, E4:8, G4:8, C4:8]" ++ lift^5;
  else
    songMiddleSynth = songMiddleSynth ++ "[F4:8, A4:8, C5:8, A4:8, E4:8, G4:8, C4:8]" ++ lift;
  done
done

synthSolo1st4 = "";
bassAccompaniment1st4 = "";

sustainedLowC = "[{C2/C3}:1]";
sustainedLowF = sustainedLowC^-7;
sustainedLowG = sustainedLowF^2;
sustainedLowBb = sustainedLowF^5;
sustainedLowA = sustainedLowBb^-1;
sustainedLowAb = sustainedLowA^-1;

descending3rd16ths = "[D5:16, B5:16, G4:16]";
descending3rd4th16ths = "[E5:16, C5:16, G4:16]";
risingMinor3rd16ths = "[A5:16, C5:16]";
risingSemitone16ths = "[E4:16, F4:16]";

i = 0;
while(i < 2) do
  synthSolo1st4 = synthSolo1st4 ++ descending3rd16ths ** 2 ++ descending3rd4th16ths ** 2
    ++ descending3rd16ths ** 3 ++ descending3rd16ths^-2 ** 3 ++ risingMinor3rd16ths;
  bassAccompaniment1st4 = bassAccompaniment1st4 ++ sustainedLowC ** 2;
  i = i + 1;
done

      
bassOutro = sustainedLowG ** 2 ++ sustainedLowC ** 3;
synthOutro = "[.:8dot, C4:8dot, D4:8, E4:8dot, C4:8dot, D4:8]"
  ++ "[E4:8dot, G4:8dot, E4:8, D4:8dot, C4:8dot, D4:8]"
  ++ "[D4:4dot, E4:4dot, C4:4]"
  ++ "[C4:1]" ** 2;

sequencing = 5;
while(sequencing != 0) do
  switch(sequencing)
    case 1:
      playConcurrent(
        1: bassOutro,
        2: synthOutro^12,
        3: fourBarDrumLoop,
        4: "[C4:4, .:4, .:4, .:4]" ++ oneBarRest ** 2 ++ "[.:4, .:4, .:8, G5:8, C5:8, C4:8, C4:4]"
      );
      sequencing = 0;
    esac
    case 2:
      playConcurrent(
        1: bassAccompaniment1st4,
        2: synthSolo1st4,
        3: fourBarDrumLoop,
        4: "[C4:4, .:4, .:4, .:4]" ++ oneBarRest ** 2 ++ "[.:4, .:4, .:8, G5:8, C5:8, C4:8]"
      );
      sequencing = 1;
    esac
    case 3:
      playConcurrent(
        1: (bass8ths ++ "[.:1]" ** 8),
        2: songMiddleSynth,
        3: fourBarDrumLoop ++ twoBarDrumLoopDotted ++ fourBarDrumLoop ++ oneBarDrumLoop ** 2,
        4: "[C3:4]"
      );
      sequencing = 2;
    esac
    case 4:
      playConcurrent(
        1: (bass8ths ++ "[.:1]" ** 8),
        2: songMiddleSynth,
        3: fourBarDrumLoop ++ twoBarDrumLoopDotted ++ fourBarDrumLoop ++ oneBarDrumLoop ** 2,
        4: "[C3:4]"
      );
      sequencing = 3;
    esac
    case 5: 
      playConcurrent(
        1: songIntroBass,
        2: songIntroSynth,
        3: fourBarRest ** 2 ++ fourBarDrumLoop ** 2,
        4: songIntroToms
      );
      sequencing = 4;
    esac
  done
done      




